plugins {
    id 'java'
    id 'org.springframework.boot' version '3.4.2'
    id 'io.spring.dependency-management' version '1.1.7'
}

group = 'tally'
version = '0.0.1-SNAPSHOT'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

ext {
    version_mysqldatabase = '9.2.0'
}

dependencies {
    // spring starter
    implementation "org.springframework.boot:spring-boot-starter-data-jpa"
    implementation "org.springframework.boot:spring-boot-starter-web"
    
    // db
    runtimeOnly "com.h2database:h2"
    implementation "com.mysql:mysql-connector-j"

    // lombok
    compileOnly "org.projectlombok:lombok"
    annotationProcessor "org.projectlombok:lombok"

    // rest-assured
    testImplementation "io.rest-assured:rest-assured"

    // test-containers
    testImplementation 'org.springframework.boot:spring-boot-testcontainers'
    testImplementation "org.testcontainers:mysql"
    testImplementation "org.testcontainers:junit-jupiter"

    // test
    testImplementation "org.springframework.boot:spring-boot-starter-test"
    testRuntimeOnly "org.junit.platform:junit-platform-launcher"
}

// ---------------------------
// 기본 test Task (전체 테스트)
// ---------------------------
tasks.named('test', Test) {
    useJUnitPlatform()
}

// ---------------------------
// 1) unitTest Task
// ---------------------------
task unitTest(type: Test) {
    group = "verification"
    include '**/unit/**'
    testClassesDirs = sourceSets.test.output.classesDirs
    classpath = sourceSets.test.runtimeClasspath
    useJUnitPlatform()
}

// ---------------------------
// 2) integrationTest Task
// ---------------------------
task integrationTest(type: Test) {
    group = "verification"
    include '**/integration/**'
    testClassesDirs = sourceSets.test.output.classesDirs
    classpath = sourceSets.test.runtimeClasspath
    useJUnitPlatform()
}

// ---------------------------
// 3) acceptanceTest Task
// ---------------------------
task acceptanceTest(type: Test) {
    group = "verification"
    include '**/acceptance/**'
    testClassesDirs = sourceSets.test.output.classesDirs
    classpath = sourceSets.test.runtimeClasspath
    useJUnitPlatform()
}
